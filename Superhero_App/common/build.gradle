buildscript {
    repositories {
        maven {
            url 'https://dl.bintray.com/jetbrains/kotlin-native-dependencies'
        }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-native-gradle-plugin:$kotlin_native_version"
    }
}


apply plugin: 'kotlin'
apply plugin: 'konan'

configurations {
    ktlint
}

dependencies {
    ktlint "com.github.shyiko:ktlint:$ktlint_version"
    implementation "org.jetbrains.kotlin:kotlin-stdlib:${kotlin_version}"
    testImplementation "junit:junit:$junit_version"
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:${kotlin_version}"
    testImplementation "org.mockito:mockito-core:$mockito_core_version"
    testImplementation "com.nhaarman:mockito-kotlin:$mockito_kotlin_version"
}

konanArtifacts {
    framework('Common', targets: ['iphone', 'iphone_sim']) {
        srcDir './src/main/kotlin'
    }

    dynamic('common_android', targets: ['android_arm32' ,'android_arm64']) {
        srcDir './src/main/kotlin'
    }
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    main = "com.github.shyiko.ktlint.Main"
    classpath = configurations.ktlint
    args "src/**/*.kt"
}
check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    main = "com.github.shyiko.ktlint.Main"
    classpath = configurations.ktlint
    args "-F", "src/**/*.kt"
}